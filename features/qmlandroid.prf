isEmpty(QMLANDROID_PREFIX): \
    QMLANDROID_PREFIX = $$[QT_INSTALL_PREFIX]/src/qmlandroid

pkg_paths = $$files($$QMLANDROID_PREFIX/java/src/qt/android/*)
for(pp, pkg_paths): \
    pkg_names += $$basename(pp)

for(pkg, QMLANDROID_PACKAGES) {
    pkg_path = $$QMLANDROID_PREFIX/java/src/qt/android/$$pkg
    !exists($$pkg_path): \
        error("Unknown QML for Android package '$$pkg'. Available packages: $$pkg_names")
    
    qmlandroid_sources_$${pkg}.files = $$pkg_path/*
    qmlandroid_sources_$${pkg}.path = /src/java/qt/android/$$pkg
    INSTALLS += qmlandroid_sources_$$pkg
}

isEmpty(ANDROID_PACKAGE_SOURCE_DIR) {
    ANDROID_PACKAGE_SOURCE_DIR = $$QMLANDROID_PREFIX/templates
} else {
    template_files = AndroidManifest.xml build.gradle
    for(f, template_files) {
        !exists($$ANDROID_PACKAGE_SOURCE_DIR/$$f): \
            error("Missing file in Android package source directory: $$f")
    }
}
